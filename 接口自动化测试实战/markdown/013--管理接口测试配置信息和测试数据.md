前面讲解了如何编写json格式和xml格式的接口测试脚本，本次课程将带领大家学习如何把接口测试中的配置信息和测试进行统一管理。为了完成这个目标拆分了3个task。

  * 使用yaml文件管理配置信息
  * 使用csv文件管理测试数据
  * 对敏感信息进行脱敏处理

## yaml文件管理配置信息

因为调用的是 WireMock 模拟的接口，所以我们用不同的端口号来模拟不同的环境地址。

Dev 环境的 baseUrl 是`http://localhost:9090,` stable环境的 baseURl
为了能在不同环境中自动运行，我们可以把应用的 baseUrl 配置到 yaml 文件中按环境进行区分，环境的值设置到环境变量中，那么在配置自动化脚本到
CI/CD 平台的时候，只需指定环境变量值就可以自动切换到不同环境运行 Case。

首先还是通过 WireMock 模拟一个 GET 请求接口，且接口的用户名、密码才能访问。mapping 件内容如下所示：

    
    
    {
      "request": {
        "method": "GET",
        "url": "/api/getData",
        "basicAuth": {
          "username": "apiUsername",
          "password": "apiPassword"
        }
      },
      "response": {
        "status": 200,
        "body": "get api data successfully"
      }
    }
    

接着创建`config.yaml`文件，yaml 文件内容如下

    
    
    active: dev
    dev:
      db:
        url: jdbc:mysql://127.0.0.1:3306/apitestdb
        user: root
        password: root                         //数据库连接信息
      mockServerUrl: http://localhost:9090/
    stable:
      db:
        url: jdbc:mysql://127.0.0.1:3306/apitestdb
        user: root
        passowrd: root
      mockServerUrl: http://localhost:9099/     //被测接口的baseUri
    

此 yaml 文件是在课程《Groovy操作文件》中创建中，此次只是多添加了mockServerUrl 配置。如下是获取 config
信息的代码，里面调用了
FileService，这个类也是在《Groovy操作文件》课程中创建的，没有创建的同学请参考前面课程的内容编写FileService，否则整个调用会失败。

    
    
    class ConfigParser {
        FileService fileService
    
        ConfigParser() {
            fileService = new FileService()
        }
    
        def getConfigs(String configFilePath) {
            def configs = fileService.getConfigs(configFilePath)       //传入yaml文件的路径，然后获取整个yaml文件的内容
            def sysEnv = System.getenv("ACTIVE_ENV")               //读取名称为“ACTIVE_ENV”的环境变量
            def active = sysEnv ? sysEnv : configs.active          //如果环境变量存在，则读取环境变量的值，如果环境变量不存在，则读取yaml文件中active值
            configs.putAll(configs.get(active))   
            //通过active值获取对应环境的配置信息，例如如果active的值是“dev“，那么自动化脚本在运行时就会获取dev下面配置的db连接信息和mockServerUrl
            configs
        }
    
        def getGlobalConfig() {
            def configs
            configs =getConfigs("src/test/resources/com/github/config/config.yml")
            configs
        }
    }
    

可以看到这里设置了环境变量 ACTIVE _ENV，当 ACTIVE_ ENV 为空时，默认获取 config 文件中设置的 ative
值作为环境值，当显示设置 ACTIVE _ENV 的值时， ACTIVE_ ENV 的值为环境值。调用接口的case如下，接口的 baseUrl 是从
yaml 文件中读取

    
    
    class GetDataClient {
        ConfigParser configParser
        def configs
    
        GetDataClient() {
            configParser = new ConfigParser()
            configs = configParser.getGlobalConfig()      //获取yaml文件配置信息
        }
    
        void getData() {
            def res = given().baseUri((String) configs.mockServerUrl)     //调用接口时，接口的baseUri来自yaml文件
                    .auth().preemptive().basic("apiUsername", "apiPassword")
                    .when()
                    .get("/api/getData")
                    .then().assertThat().statusCode(200)
                    .extract().response().getBody().asString()
            println res
        }
    
        @Test()                         //使用junit的@Test注解调用上面编写的getData方法
        void callGetData() {
            getData()
        }
    }
    

运行上面的 Case 如下图所示，调用成功，因为 Config 中默认的 active 值是 Dev，Dev 对应的接口是 9090，此时我们修改
Config 中 active 的值为 stable，再次运行，Case 失败，因为 stable 下面配置的 mockServerUrl
的地址接口是9099，说明环境切换成功。

![](https://images.gitbook.cn/15758729182871)

大家可以尝试此时修改 mock server 中 Application class 中的端口为 9099，重启 mock server，再次运行上面的
Case，Case应该运行成功。

前面的切换是通过修改 config 中 active 值实现的，Jekins 配置时会通过显示设置ACTIVE_ENV 值来切换环境。

上面我们完成了接口 baseUrl 的配置，但对于测试数据（例如调用接口的用户名和密码）还是写死在代码中，接下来将带领大家学习如何通过 CSV
文件管理测试数据。

## 使用 CSV 文件管理测试数据

实际项目中我们肯定会遇到这样的测试场景：被测系统中有很多接口，某些接口只能某一个角色有权限进行调用，那么对于权限这部分进行手动测试时，我们的步骤一般是不同的角色准备不同的用户名，然后用对应的用户调用权限范围内的接口，期望都能调用成功，调用权限外的接口，期望返回
401 或者 403 的错误码。

针对这样的场景在做接口自动化时，可以把需要的所有测试用户数据放到统一的 CSV
文件中，调用接口的时候传入角色名称即可，这样就可以轻松验证不同角色调用不同接口后返回的 Response 是否符合期望。

以上面 Mock 的接口为例，接口调用中用到了用户名、密码信息。因为是 Mock
的接口并没有涉及用户角色的内容，这里我们假设一个角色给这个用户，验证的场景是这个角色能调用上面 Mock 的 getData
接口。为了完成这个场景的自动化，首先创建`user.csv`文件，把用户信息都存放在`user.csv`文件中，CSV 文件内容如下，使用","作为分割符。

    
    
    roleName,username,password
    ForGetDataApi,apiUsername,apiPassword
    ForOther,xxx,yyy
    

创建 TestDataService Class，该 Class 中直接使用了课程《Groovy操作文件》中创建的
FileService，TestDataService 中实现了通过角色名字获取用户信息，代码内容如下

    
    
    class TestDataService {
        FileService fileService
    
        TestDataService() {
            fileService = new FileService()
        }
    
        def getUserFileData() {
            def userData = fileService.getCsvFileContent("src/test/resources/com/github/data/user.csv", ",")   //读取csv文件内容，“，”作为分割符
            userData
        }
    
        def getUserDataByRole(roleName) {
            getUserFileData().find { it -> it.roleName == roleName }    //通过find方法按RoleName查找对应的用户名、密码信息
        }
    }
    

重新编写调用接口的代码，内容如下，参数化接口的用户名、密码。

    
    
    class GetDataClient {
        ConfigParser configParser
        TestDataService testDataService
        def configs
        def users
        GetDataClient() {
            configParser = new ConfigParser()
            testDataService = new TestDataService()
            configs = configParser.getGlobalConfig()       //获取yaml文件配置信息
            users = testDataService.getUserDataByRole("ForGetDataApi")   //按照角色名称“ForGetDataApi”获取用户信息
        }
        void getDataWithCsvUser() {
            def res = given().baseUri((String) configs.mockServerUrl)
                    .auth().preemptive().basic(users.username, user.password))  //接口调用时使用的用户名称和密码是从csv文件中获取的
                    .when()
                    .get("/api/getData")
                    .then().assertThat().statusCode(200)
                    .extract().response().getBody().asString()
            println res
        }
        @Test()
        void callGetDataWithCsvUser() {
            getDataWithCsvUser() //编写测试脚本验证上面的接口调用是否正确
        }
    }
    

运行上面的case，可以看到运行成功，修改 user.csv 文件，改为错误的用户名，再次运行，运行失败，说明接口调用的时候确实是从 CSV
文件中获取的用户信息。

![](https://images.gitbook.cn/15758729182890)

通过上面的学习，我们掌握了如何通过yaml文件管理配置信息和如何通过csv文件管理测试数据。在实际项目中只要能根据环境动态管理好这两类信息那么就可以实现多环境自动化切换运行。当然实际项目中涉及的内容会比上面的demo复杂，例如测试数据初始化、测试数据的清理等。但思路都是一样的，大家可以用这个思路持续优化项目中的接口自动化测试脚本。

上面通过csv文件管理了应用所需的用户信息，但敏感信息password是明文存放在csv文件中的，需要进行脱敏处理。接下来将带领大家学习一种简单的脱敏方法

## 对敏感信息进行脱敏处理

创建Secret Class，编写加密和解密的方法，代码细节如下

    
    
    class Secret {
        private static byte[] salt = [ (byte) 0xA9, (byte) 0x9B, (byte) 0xC8, (byte) 0x32, (byte) 0x56, (byte) 0x35, (byte) 0xE3, (byte) 0x03]
    
    //包的加密方法，调用的时候需要传入key和明文，方法返回加密后的密文
    
        static String encrypt(String secretKey, String plainText) throws Exception      
        {    KeySpec keySpec = new PBEKeySpec(secretKey.toCharArray(), salt, 19)
            SecretKey key = SecretKeyFactory.getInstance("PBEWithMD5AndDES").generateSecret(keySpec)
            AlgorithmParameterSpec paramSpec = new PBEParameterSpec(salt, 19)
            Cipher cipher = Cipher.getInstance(key.getAlgorithm())
            cipher.init(Cipher.ENCRYPT_MODE, key, paramSpec)
            String charSet = "UTF-8"
            byte[] input = plainText.getBytes(charSet)
            byte[] output = cipher.doFinal(input)
            return new String(Base64.getEncoder().encode(output))}
    
     //包的解密方法，调用的时候需要传入key和加密后的密文
    
        static String decrypt(String secretKey, String encryptedText) throws Exception          
        {    KeySpec keySpec = new PBEKeySpec(secretKey.toCharArray(), salt, 19)
            SecretKey key = SecretKeyFactory.getInstance("PBEWithMD5AndDES").generateSecret(keySpec)
            AlgorithmParameterSpec paramSpec = new PBEParameterSpec(salt, 19)
            Cipher cipher = Cipher.getInstance(key.getAlgorithm())
            cipher.init(Cipher.DECRYPT_MODE, key, paramSpec)
            byte[] enc = Base64.getDecoder().decode(encryptedText)
            byte[] utf8 = cipher.doFinal(enc)
            String charSet = "UTF-8"
            return new String(utf8, charSet)}
    
    //写了个main方法来调用上面两个方法
         static void main(String[] args) throws Exception      
        {   String key = "apiTestStudy"               //设置key值，这里使用的是对称加密，加密和解密时使用相同的key值即可，key具体的值自己可以任意设置
            String plain = "apiPassword"              //明文密码
            String enc = encrypt(key, plain)          //加密后的密码
            System.out.println("Original text: " + plain)
            System.out.println("Encrypted text: " + enc)
            String encs="/DgMfJk8Y7YvMrMBiD881A=="    //加密后的密文 
            String plainAfter = decrypt(key, encs)    //密文解密
            System.out.println("Original text after decryption: " + plainAfter)   //解密后的明文
        }
    }
    

加密和解密的写法比较固定，这里不做更多介绍，如果自己项目中使用其他方式进行脱敏也可以参照项目标准进行修改。main方法里面我们可以设置秘钥key的值，如上设置的key="apiTestStudy",plain为需要加密的明文，encs为需要解密的密文，运行main方法就会打印出加密后的密文和解密后的明文。如上所示"apiPassword"加密后的密文为"/DgMfJk8Y7YvMrMBiD881A==",接下来我们需要对user.csv里面password进行脱敏处理。首先替换user.csv文件中的明文password为密文password

    
    
    rolename,username,password
    ForGetDataApi,apiUsername,/DgMfJk8Y7YvMrMBiD881A==
    ForOther,xxx,yyy
    

TestDataService中新增通过用户名获取明文密码的方法，因为接口调用的时候需要的还是解密后的密码，所以从csv文件读取密文后，需要进行解密处理。代码如下

    
    
     def getPasswordByUserName(userName) {
         // 这个方法是通过用户名称获取解密后的用户密码
           Secret.decrypt( "apiTestStudy",(String)getUserFileData().find{it -> it.username == userName}.password)   //Secret中都是静态方法，所以可以直接类名.方法名进行调用
        }
    

修改GetDataClient中调用接口的代码，通过getPasswordByUserName获取明文密码供接口调用，代码如下

    
    
    class GetDataClient {
        ConfigParser configParser
        TestDataService userTestData
        def configs
        def users
    
        GetDataClient() {
            configParser = new ConfigParser()
            userTestData = new TestDataService()
            configs = configParser.getGlobalConfig()                      //获取yaml中的配置信息
            users = userTestData.getUserDataByRole("ForGetDataApi")      //通过角色获取用户信息，用户信息包含用户名称和加密后的用户密码
        }
     void getDataWithEncryptPassword() {
            def res = given().baseUri((String) configs.mockServerUrl)
                    .auth().preemptive().basic(users.username, userTestData.getPasswordByUserName(users.username))   //这里调用userTestData.getPasswordByUserName解密
                    .when()
                    .get("/api/getData")
                    .then().assertThat().statusCode(200)
                    .extract().response().getBody().asString()
            println res
        }
         @Test()
        void callGetDataWithEncryptPassword() {
            getDataWithEncryptPassword()  //编写测试脚本验证上面的接口调用是否正确
        }
    }
    

运行case如下图所示运行成功，说明整个加密、解密过程正确。

![](https://images.gitbook.cn/15758729182918)

## 总结

至此今天的课程就结束了，今天主要带大家学习了如何统一管理配置信息和测试数据以及敏感数据脱敏。

实际项目中配置信息中如果有敏感信息也需要脱敏处理，例如yaml文件中配置不同环境的数据库连接密码。大家可以自己尝试把yaml文件中的数据库配置信息进行脱敏处理，然后重新执行（Groovy操作数据库）课程中用例，如果能成功运行则说明加密、解密过程正确。下节课将带领大家学习如何搭建jenkin运行自动化脚本。

