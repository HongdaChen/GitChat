上一节课程中我们学习了如何搭建被测web应用，分析web应用中测试场景以及哪些适合放到接口自动化中，接着clone了接口测试代码并运行了已经写好的自动化案例。本次课程将带领大家来了解整个自动化case是如何实现的。为了完成本次课程目标，我分了4个task

  * 了解接口测试项目目录结构
  * 从case出发查看代码
  * 以其中一个场景为例查看case后面的代码实现细节
  * 初始化数据做了哪些工作

### 了解接口测试项目目录结构

接口测试项目structer，如下图所示

![](https://images.gitbook.cn/15758728725357)

scenario package下包含如下package

  * repository存放实现与数据库的交互的class
  * requestBody实现构造各种接口请求的body
  * service存放实现各种数据逻辑处理的class，例如从数据库获取的数据需要进行二次处理才能在case中直接调用

testData package下存放获取或者初始化测试数据的class

util package下面存放一下公共的class，例如Secret Class，Configparse Class

  * 实战项目配置信息都从crownConfig.yml中获取，且对敏感数据（db password）进行了脱敏处理
  * 需要全局初始化的user信息存放在role.csv 和user.csv中

### 从case出发查看代码

scenario部分
总共实现了6个测试Class（AddUserTest，EditUserTest,UpdateUserStatusTest，ResetPasswordTest,SearchUserTest,UserRightTest）

AddUserTest Class中测试了系统的三个角色（systemManager,userManager,roleManager）都可以成功添加用户

添加用户正常场景（AddUserTest）

    
    
    @Category(CrownTest)     //这里设置此Class属于CrownTest Category，作用是拆分testSuite
    class AddUserTest extends Specification {
        UserClient userClient
        UserService userService
        UserRepository userRepository
        def loginName = ""
    
        def setup() {                      //每个case执行前都会先执行setup中的内容
            userClient = new UserClient()
            userService = new UserService()
            userRepository = new UserRepository()
        }
    
        def cleanup() {                   //每个case执行后都会执行cleanup中的内容，这里每次case执行完成后，删除添加的user，达到清理测试数据的目标
            userService.deleteUser(loginName)
        }
    
        def "should add user successfully when filling all required information"() {
            def roleIdList = userService.generateUserRoleList(addedUserRoleName)    
            // 上面的代码是根据被添加的用户角色生成对应的角色ID List，页面上创建一个user，选中对应角色时看到的是角色名称，而实际插入数据库的数据是角色对应的Id值，所以这里包了一个方法根据角色名称得到对应的roleIdList
    
            given: "generate add user api request body"
            loginName = userService.generateUniqueLoginName()     
            //这里包了一个方法生成不重复的登陆名称，因为web应用限制了添加用户时，不能添加已经存在的登陆名称，所以为了保证case多次执行都能成功，我们需要包一个方法来生成不重复的登陆名称
    
            def body = new UserBody()
                    .setUserLoginName(loginName)
                    .setUserNickName("abc123NickName")
                    .setUerPhone("18181971234")
                    .setUserEmail("test@163.com")
                    .setRoleIdList(roleIdList)
                    .getAddUserBody()                //这里采用build模式构造接口的reqeust body
            when: "call add user api interface"
            userClient.addUser(addUserRoleName, body)    //这里addUser()方法实现调用添加user的接口
                    .statusCode(201)
            then: "should add user in db successfully"
            Assert.assertTrue(userService.ifUserExist(loginName))   //接口调用后判断被添加的用户确实被添加了
            where:
            addUserRoleName | addedUserRoleName      //这里通过data-driven的方式覆盖了系统中三个角色都能添加user的测试场景
            "systemManager" | "roleManager"
            "userManager"   | "roleManager"
            "roleManager"   | "roleManager"
        }
    
        def "should add user failed when not filling all required information"() {
            def roleIdList = userService.generateUserRoleList(addedUserRoleName)   //同上，这里是通过传入被添加的角色名称生成对应的roleList
            given: "generate add user api request body"
            loginName = userService.generateUniqueLoginName()        //添加一个唯一的登陆名称
            def body = new UserBody()
                    .setUserLoginName(loginName)
                    .setUserNickName(nickName)
                    .setUerPhone(phone)
                    .setUserEmail(email)
                    .setRoleIdList(roleIdList)
                    .getAddUserBody()               //构建添加user的requst body
            when: "call add user api interface"
            userClient.addUser(addUserRoleName, body)
                    .statusCode(400)
            then: "should add user in db successfully"
            Assert.assertFalse(userService.ifUserExist(loginName))     //这里因为都是异常测试场景，所以校验被添加的用户没有成功被添加
            where:
            nickName   | phone         | email          | addedUserRoleName | addUserRoleName
            ""         | "18181971234" | "test@163.com" | "roleManager"  | "systemManager"
            "nickName" | ""            | "test@163.com" | "roleManager"  | "systemManager"
            "nickName" | "18181971234" | ""             | "roleManager"  | "systemManager"
            "nickName" | "18181971234" | "test@163.com" | ""         | "systemManager"
    
            // 添加用户异常测试场景中，当email为空时，系统没有报错，仍然允许添加用户，属于系统issue，所以添加用户的异常场景会有一个失败
        }
    }
    

编辑用户场景，代码如下

    
    
    @Category([CrownTest])
    class EditUserTest extends Specification {
        UserService userService
        UserRepository userRepository
        UserClient userClient
        def loginName="userForEdit"
    
        def setup() {
            userService = new UserService()
            userRepository = new UserRepository()
            userClient = new UserClient()
        }
    
        def cleanup() {
            userService.deleteUser(loginName)     //每一次case执行完成后都会清理掉名称为“userForEdit”的用户，where里面有三行测试数据，等同于三个case
        }
    
        def "should update user details successfully"() {
            given: "create a exist user"
            userService.addUser(loginName,"abc123","systemManager")     
        //为了测试编辑用户场景，需要提前创建一个存在的用户，这里userService中包了一个addUser的方法，这里是通过直接操作数据库的方式添加user，传入登陆名称，密码，角色名称即可添加一个user
    
            def userInfo=userRepository.getUserInfoByUserName(loginName)           //获取已经创建的用户信息
            and: "generate edit user body"
            def reqBody = new UserBody()
                    .setUserIdForEditUser(userInfo.uid)      //编辑用户的接口里面需要传递被修改用户的uid
                    .setUserLoginName(loginName)
                    .setUserNickName(newNickName)
                    .setUerPhone(newPhone)
                    .setUserEmail(newEmail)
                    .setRoleIdList(userService.generateUserRoleList("roleManager"))   //编辑用户的request body中设置把用户都修改为“roleManage”角色
                    .getAddUserBody()
            when: "edit user details"
            userClient.editUser(updateUserRoleName,userInfo.uid,reqBody)
                      .statusCode(200)                     //调用编辑用户接口，编辑用户信息
            then: "should edit successfully"
            def newUserInfo=userRepository.getUserInfoByUserName(loginName)
            assertThat(newUserInfo.nickName).isEqualTo(newNickName)
            assertThat(newUserInfo.phone).isEqualTo(newPhone)
            assertThat(newUserInfo.email).isEqualTo(newEmail)      //校验用户新的信息与期望被修改的信息一致
            assertThat(userRepository.getUserRole(loginName).role_id).isEqualTo(userRepository.getRoleInfoByRoleName("roleManager").id)
            // 校验被修改的用户角色确实是roleManager
            where:
            updateUserRoleName|newNickName|newPhone|newEmail
            "systemManager"| "newNickName"|"18181991122"|"newEmail@163.com"
            "userManager"| "newNickName"|"18181991122"|"newEmail@163.com"
            "roleManager"| "newNickName"|"18181991122"|"newEmail@163.com"
        }
    }
    

修改用户状态场景

    
    
    @Category([CrownTest])
    class UpdateUserStatusTest extends Specification {
        UserClient userClient
        UserService userService
        UserRepository userRepository
        def testUserLoginName = "statusTest"
    
        def setup() {
            userClient = new UserClient()
            userService = new UserService()
            userRepository = new UserRepository()
        }
    
        def clean() {
            userService.deleteUser(testUserLoginName)    //case运行完成后会删除被测用户
        }
    
        def "should update user status successfully"() {
            given: "add a test user"
            userService.addUser(testUserLoginName)   //添加一个用户，用于后面修改状态测试
            when: "update user status"
            def userInfo = userRepository.getUserInfoByUserName(testUserLoginName)    //获取被测用户信息
            userClient.updateUserStatus(updateStatusUserRoleName, userInfo.uid, userInfo.status)    //调用修改状态接口，修改用户状态
            then: "should update user status successfully"
            assertThat(userInfo.status).isNotEqualTo(userRepository.getUserInfoByUserName(testUserLoginName).status)   //校验修改状态后的状态与修改前的状态不一致
            where:
            updateStatusUserRoleName | placeHolder
            "systemManager"          | ""
            "roleManager"            | ""
            "userManager"            | ""        //测试三种角色都能修改用户状态
        }
    }
    

重置用户密码场景

    
    
    @Category([CrownTest])
    class ResetPasswordTest extends Specification {
        UserService userService
        UserClient userClient
        UserRepository userRepository
        def loginName = "userForReset"
    
        def setup() {
            userService = new UserService()
            userClient = new UserClient()
            userRepository = new UserRepository()
        }
    
        def cleanup() {
            userService.deleteUser(loginName)       //case运行完成后会删除被测用户
        }
    
        def "should reset user password successfully"() {
            given: "create a user with password"
            userService.addUser(loginName, password)   //添加一个被测用户，用户密码都是abc456 
            when: "reset user password"
            def uid = userRepository.getUserInfoByUserName(loginName).uid     //获取用户的uid，用户重置密码时需要
            userClient.resetUserPassword(resetPasswordRoleName, uid)          //调用接口重置用户密码
            then: "should user's password is equal to loginName"
            assertThat(userRepository.getUserInfoByUserName(loginName).password).isEqualTo(userService.getEncryptPassword(loginName, loginName))
           //被测web应用如果重置密码，会把密码设置为登陆名称，因为数据库存储密码时做了加密处理，这里判断数据库存储的密码和用登录名加密后的内容一致
           //userService.getEncryptPassword使用的加密方式是从被测web应用中copy的
            where:
            password | resetPasswordRoleName     //测试系统中的三种角色都可以重置用户密码
            "abc456" | "systemManager"
            "abc456" | "userManager"
            "abc456" | "roleManager"
    
            //修改用户密码场景没有写自动化，大家学习后可以自己尝试编写
        }
    }
    

检索用户场景

    
    
    @Category([CrownTest])
    class SearchUserTest extends Specification {
        UserClient userClient
        UserService userService
    
        def setup() {
            userClient = new UserClient()
            userService = new UserService()
        }
    
        def "should search user successfully"() {
            given: "no given"
            when: "search user"
            def response = userClient.searchUser(searchUserRoleName, loginName, nickName, status)  
            //调用检索接口
            then: "should get all valid user"
            assertThat(response.result.total).isEqualTo(userService.getUserNumbersForSearch(loginName, nickName, status))
            //调用检索接口返回的response中的total个数与数据库中查询的数量一致则说明检索接口正确
            where:
            searchUserRoleName | loginName | nickName | status
            "systemManager"    | "name"    | ""       | ""
            "systemManager"    | ""        | "abc"    | ""
            "systemManager"    | ""        | ""       | 0
            "roleManager"    | ""        | ""       | 1       //这里使用了roleManger与userManager进行检索，主要是顺便覆盖三种角色都有检索用户接口权限
            "userManager"    | "crown"   | "crown"  | 0
            "systemManager"    | ""        | ""       | ""
        }
    
        def "should search user with limit successfully"() {
            given: "no given"
            when: "search user with limit 20"
            def response = userClient.searchUserWithLimit(searchUserRoleName,limit)   //检索接口的时候设置检索的总量是20
            then: "should get valid user"
            assertThat(response.result.size).isEqualTo(limit)   //校验返回的response中用户数量是20
            where:
            searchUserRoleName|limit
            "systemManager"|20
        }
    }
    

按前面的分析“不同角色权限测试”应该包含三个方面

1：角色可以调用所属权限范围内的所有接口

2：角色禁止调用所属权限范围外的任何接口

3：角色变更后权限正确

但UserRightTest这个class中无任何内容，因为上面三个检查点在前面其他case中已覆盖。比如第一个测试点，以添加用户case为例，已经测试了使用三个不同的角色创建用户。对于第二个测试点，因为这三个角色都具备”用户管理“菜单权限，所以不存在”禁止调用所属权限范围外的任何接口“的测试场景。对于第三个测试点，EditUser中已经覆盖更新用户角色场景，即角色变更的场景，所以不用单独验证修改用户角色后的权限是否正确。这就是前面讲的巧妙覆盖的技巧。因为系统中存在三种角色，那么在初始化数据的时候就准备好这三种角色用户，那么在做业务场景测试时，可以组合不同的角色进行调用，这样权限这块就顺便覆盖了。初始化数据代码细节后面会详细介绍。

## 查看case后面的实现细节

我们以添加用户为例来看case后面的代码实现细节，添加用户接口调用如下图所示

    
    
    userClient.addUser(addUserRoleName, body)    
                    .statusCode(201)
    
    //下面是userClient.addUser 实现
      def addUser(def addUserRoleName, def body) {
            loginClient.clientWithSpecialRole(addUserRoleName)
                    .body(body)
                    .when()
                    .post('/users')
                    .then()
                    .assertThat()
        } 
    //可以看到addUser里面调用了loginClient的方法，所以我们查看LoginClient Class
    class LoginClient {
        GlobalUserService globalUserService
        ConfigParse configParse
    
        LoginClient() {
            globalUserService = new GlobalUserService()      
            configParse = new ConfigParse()       //这里调用Configparse，因为所有环境相关的配置信息都放到了yaml文件中
        }
    
        def clientWithSpecialRole(roleName) {
            def userInfo = globalUserService.getUserInfoByRole(roleName)     //根据角色获取用户信息
            setDefaultUriAndContentType()   //该方法和getTokenByUser方法都需要设置baseUri，header等，所以把公共的内容抽取了一个“setDefaultUriAndContentType”方法，减少冗余 
                    .header("Authorization", getTokenByUser(userInfo.userName, userInfo.password))     //查看web应用可以看到所有接口调用都需要在header中设置token
        }
    
        private def getTokenByUser(userName, password) {
            HashMap userMap = new HashMap()           //这里是根据用户名称和密码生成token的方法
            userMap.put("loginName", userName)
            userMap.put("password", password)
            def token = setDefaultUriAndContentType()
                    .body(userMap)
                    .post("/account/token")
                    .then().assertThat().statusCode(200)
                    .extract().response().path("result.token")
            token = "Bearer " + token
            token
        }
    
        private def setDefaultUriAndContentType(contentType = "application/json") {
            given().baseUri((String) configParse.getGlobalConfig().baseUrl)
                    .header("Content-Type", contentType)
        }
    }                   
    

以上就是对case实现后面的代码理解，另外接口测试中涉及密码的信息都进行了脱敏处理，采用的方式就是课程12“统一管理配置信息和测试数据”课程中的脱敏方法。

## 初始化数据完成了哪些任务

以下是初始化测试数据（测试场景需要的三种角色用户）代码

    
    
    package com.github.crown.testdata.service
    
    import com.github.crown.scenario.userManage.repository.UserRepository
    import com.github.crown.scenario.userManage.service.UserService
    
    class GlobalUserService {
        UserRepository userRepository
        UserService userService
        com.github.crown.util.CrownFileService crownFileService      
    
        GlobalUserService() {
            userRepository = new UserRepository()
            userService = new UserService()
            crownFileService = new com.github.crown.util.CrownFileService()     //处理各类文件Class
        }
    
        private def getAllUserInfo() {
            crownFileService.getCsvFileContent("src/test/resources/com/github/crown/data/user.csv", ",")   //从文件中获取用户信息
        }
    
        private def getAllRoleMenuInfo() {
            crownFileService.getCsvFileContent("src/test/resources/com/github/crown/data/role.csv", ";")   //从文件中获取角色信息，当你熟悉这个被测web后，你会发现角色需要有权限，还需要关联对应的menuId即菜单Id，所以此表存放了角色名称与menuId的关系
        }
    
        def getUserInfoByRole(roleName) {
            getAllUserInfo().find { it -> it.roleName == roleName }
        }
    
        private void addGlobalUsers() {
            def allUserInfo = getAllUserInfo()
            allUserInfo.each {
                userService.addUser(it.userName, it.password, it.roleName)
            }
        }
    
        private void initGlobalRole() {
            def allRoleMenuInfo = getAllRoleMenuInfo()
            allRoleMenuInfo.each {
                if (!ifRoleExist(it.roleName)) {     //这里是初始化角色，需要先把角色初始化好后，才能创建用户，然后把对于的角色与用户关联，需要注意这里做了判断，如果角色已经存在则不再重复创建，这个是接口测试处理数据中需要注意的问题。
                    userRepository.insertSysRoleTable(it.roleName)
                }
            }
        }
    
        private boolean ifRoleExist(roleName) {
            userRepository.getRoleInfoByRoleName(roleName) ? true : false
        }
    
        private void initRoleMenuId() {          //角色创建后，还需要把角色与对应的菜单Id关联，那么这个角色才真正具备对应的权限，这里的作用是把角色和对于的menuId进行关联
            def allRoleMenuInfo = getAllRoleMenuInfo()
            def roleId
            allRoleMenuInfo.each {
                roleId = userRepository.getRoleInfoByRoleName(it.roleName).id
                parseMenuIdList(it.menuIdList).each { menuId ->
                    menuId
                    if (!ifRoleIdMenuIdExist(roleId, menuId)) { //这里先判断roleId与menuId是否存在，如果存在则不再添加，保证数据不重复插入，因为有些业务下，重复插入数据是不被允许的
                        userRepository.insertSysRoleMenuTable(roleId, menuId)
                    }
                }
            }
        }
    
        private def parseMenuIdList(def menuIdListString) {
            def parseString = menuIdListString.substring(1, menuIdListString.size() - 1) //这里把用逗号隔开的menuIdList转换为一个数组存放所有的menuId
            parseString.split(",")
        }
    
        private boolean ifRoleIdMenuIdExist(roleId, menuId) {
            userRepository.getRoleMenuRel(roleId, menuId) ? true : false
        }
    
        void initGlobalUser() {    //这里是包的总的方法，也是这个Class对外的行为，是个public的方法，在需要初始化数据的地方调用该方法即可，其他方法都被包在这个Class中隐藏起来
            initGlobalRole()
            initRoleMenuId()
            addGlobalUsers()
        }
    }
    

另外，以下是初始化数据中“通过数据库添加user”的代码，因为被测应用中添加用户落入数据时进行了加密处理，所以初始化数据的时候也需要用备用应用的加密方式进行加密

    
    
     def getEncryptPassword(password, loginName) {
            Md5Crypt.apr1Crypt(password, loginName)   //这里使用的是Md5加密，也是从被测应用中copy过来的，对一个内容进行加密都需要传入一个key，被测应用中使用用户登陆名称作为key
                                                      //所以对password加密时，需要传入password和loginName
        }
    
        void addUser(def loginName, def password = "abc123", def roleName = "systemManager") {  
            //上面的方法password和roleName带了默认值，作用是：调用该方法时如果不传入后面的参数，那么就用默认值
            if (!ifUserExist(loginName)) {
                //以下通过插入数据库的方式添加user，user的password是加密后的password，这里一定要处理正确，否则初始化出来的用户无法登陆被测应用
                userRepository.insertSysUserTable(loginName, loginName, getEncryptPassword(password, loginName), "test@163.com", "18181971234", 0)  
                userRepository.insertSysUserRoleTable(userRepository.getUserInfoByUserName(loginName).uid, userRepository.getRoleInfoByRoleName(roleName).id)
            }
        }
    

以上是初始化数据的内容，这里想强调一点，初始化数据要保证健壮性，比如有些信息已经存在则不再重复创建。需要达到的目的是，如果系统中测试数据被破坏或者局部破坏，那么运行初始化数据脚本就能把数据恢复到期望的状态。

以上就是本次课程的全部内容，除数据初始化这部分前面没有案例讲解外，其他所有内容实际前面都有讲解，例如测试数据的管理，配置信息的管理，脱敏处理等。本次课程学习后大家可以尝试自己添加“角色管理部分”的自动化。

